set view map
set size square
unset surface
unset clabel
set contour
set dgrid3d 100,141
set cntrparam cubicspline
set table
#
#  Define here the countour values. Each set of countours will have the same
#  color and is written in a different file
#
set cntrparam levels discrete 0.01,0.02,0.03
set output "$TMP_DIR/table.dat"
splot '$TMP_DIR/si.rho.dat' using 1:2:3 w l

set cntrparam levels discrete 0.04,0.05,0.06
set output "$TMP_DIR/table1.dat"
splot '$TMP_DIR/si.rho.dat' using 1:2:3 w l

set cntrparam levels discrete 0.07,0.08
set output "$TMP_DIR/table2.dat"
splot '$TMP_DIR/si.rho.dat' using 1:2:3 w l
#
unset table
#
#  Now define a postcript terminal
#
set encoding iso_8859_15
set terminal postscript enhanced solid color "Helvetica" 20
set output "$TMP_DIR/si.contour.ps"
#
#  prepare the countour graph
#
set size ratio 1./1.4142
set key off
alat=10.2
set border lw 3
set label "Si" at 10.6,8.7
set xrange [0:1.4142136*alat]
set yrange [0:alat]
set xlabel "r (a.u.)"
set ylabel "r (a.u.)"
#
#  Set contour labels
#
dato="0.08"
set obj 9 rect at 6.,1.3 size char strlen(dato)*0.6, char 0.6
set obj 9 fillstyle solid noborder front
set label at 6.,1.3 dato front center font "Helvetica,12" tc rgb "blue"

dato="0.07"
set obj 10 rect at 7.9,1.3 size char strlen(dato)*0.6, char 0.6
set obj 10 fillstyle solid noborder front
set label at 7.9,1.3 dato front center font "Helvetica,12" tc rgb "blue"

dato="0.06"
set obj 11 rect at 3.3,1.0 size char strlen(dato)*0.6, char 0.6
set obj 11 fillstyle solid noborder front
set label at 3.3,1.0 dato front center font "Helvetica,12" tc rgb "green"

dato="0.01"
set obj 12 rect at 3.6,6.0 size char strlen(dato)*0.6, char 0.6
set obj 12 fillstyle solid noborder front
set label at 3.6,6.0 dato front center font "Helvetica,12" tc rgb "red"

dato="0.02"
set obj 13 rect at 3.6,5.4 size char strlen(dato)*0.6, char 0.6
set obj 13 fillstyle solid noborder front
set label at 3.6,5.4 dato front center font "Helvetica,12" tc rgb "red"

dato="0.03"
set obj 14 rect at 3.6,4.9 size char strlen(dato)*0.6, char 0.6
set obj 14 fillstyle solid noborder front
set label at 3.6,4.9 dato front center font "Helvetica,12" tc rgb "red"

dato="0.04"
set obj 15 rect at 2.3,3.7 size char strlen(dato)*0.6, char 0.6
set obj 15 fillstyle solid noborder front
set label at 2.3,3.7 dato front center font "Helvetica,12" tc rgb "green"

dato="0.05"
set obj 16 rect at 3.6,3.7 size char strlen(dato)*0.6, char 0.6
set obj 16 fillstyle solid noborder front
set label at 3.6,3.7 dato front center font "Helvetica,12" tc rgb "green"

dato="0.05"
set obj 17 rect at 7.2,1.9 size char strlen(dato)*0.6, char 0.6
set obj 17 fillstyle solid noborder front
set label at 7.2,1.9 dato front center font "Helvetica,12" tc rgb "green"

dato="0.01"
set obj 18 rect at 10.8,2.3 size char strlen(dato)*0.6, char 0.6
set obj 18 fillstyle solid noborder front
set label at 10.8,2.3 dato front center font "Helvetica,12" tc rgb "red"

#
# Print the countour
#
plot "$TMP_DIR/table.dat" u 1:2 w l lw 3 lc rgb "red",\
      "$TMP_DIR/table1.dat" u 1:2 w l lw 3 lc rgb "green",\
      "$TMP_DIR/table2.dat" u 1:2 w l lw 3 lc rgb "blue"
